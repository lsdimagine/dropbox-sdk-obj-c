namespace resellers

import common
import users

alias ResellerId = String(min_length=10)

struct DomainResellerLookupArg
    domain String

    example default
        domain="it-solutions.com"

struct DomainResellerLookupResult
    reseller_ids List(ResellerId)

    example default
        reseller_ids=["RESELLER_1", "RESELLER_2"]

struct ResellerContactInfo
    title String?
    first_name common.NamePart
    last_name common.NamePart
    email common.EmailAddress
    contact_number String

    example default
        title=""
        first_name="John"
        last_name="Doe"
        email="johndoe@it-solutions.com"
        contact_number="(555) 123-4567"

struct Address
    street String
    city String
    state String?
    postal String?
    country_code String(min_length=2,max_length=2, pattern="[A-Za-z]{2}")

    example default
        street="123 4th Ave"
        city="Seattle"
        state="WA"
        postal="98104"
        country_code="US"

    example no_zip
        street="123 Main st"
        city="Dublin"
        country_code="IE"



struct ResellerCreationArg
    biz_contact ResellerContactInfo
    tech_contact ResellerContactInfo

    company_name common.NamePart
    company_domain String
    company_size UInt32
    partner_type UInt32
    num_customers UInt32
    customer_size UInt32

    address Address

    # Extra Fields that end up on the .extra_info of the reseller entity
    company_contact_number String
    services_contact ResellerContactInfo
    marketing_contact ResellerContactInfo
    channel_sales_employees UInt32
    geo_coverage String
    industries_covered String
    revenue_split String
    service_delivery_size UInt32
    services_type String
    vertical_focus String
    technical_offering_types String
    seperate_services_group Boolean
    previous_relationship Boolean
    existing_integration String


    example default
        biz_contact=default
        tech_contact=default

        company_name="IT Solutions"
        company_domain="it-solutions.com"
        company_size=1
        partner_type=1
        num_customers=1
        customer_size=1

        address=default

        company_contact_number="555 555-5555"
        services_contact=default
        marketing_contact=default
        channel_sales_employees=100
        geo_coverage="EMEA"
        industries_covered="All"
        revenue_split="90/10"
        service_delivery_size=10000
        services_type="Some"
        vertical_focus="Photos"
        technical_offering_types="Some"
        seperate_services_group=true
        previous_relationship=false
        existing_integration=""


struct ResellerCreationResult
    reseller_id ResellerId
    domain String

    example default
        reseller_id="RESELLER_1"
        domain="it-solutions.com"

union ResellerCreationError
    invalid_data
        "Data passed in didn't pass validation."

route manage/domain_lookup (DomainResellerLookupArg, DomainResellerLookupResult, Void)
    attrs
        auth="app"

route manage/create (ResellerCreationArg, ResellerCreationResult, ResellerCreationError)
    attrs
        auth="app"
