namespace files

import async
import common
import users_common

@common.PathRedactor
alias RestorePath = String(pattern="(/(.|[\\r\\n])*)|(ns:[0-9]+(/.*)?)")

#
# Patches Metadata
#
patch struct FileSharingInfo

    @common.InternalOnly
    is_parent_shared_folder_read_only Boolean
        "Whether the parent shared folder is ready only. Not set when no parent shared
        folder exists."

    @common.InternalOnly
    last_modifier ModifierAccountInfo?
        "The last Dropbox account to modify the file content. This field will be null if
        the user's account has been deleted."
        struct
            account_id users_common.AccountId
                "The user's unique Dropbox ID."
            user_id String
                "The user's deprecated, unencoded Dropbox ID."
            display_name String
                "The user's display name."
            email String
                "The user's e-mail address. Do not rely on this without checking the
                :field:`email_verified` field. Even then, it's possible that the user
                has since lost access to their e-mail."
            email_verified Boolean
                "Whether the user has verified their e-mail address."

            example default
                account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
                user_id = "123456"
                display_name = "Franz Ferdinand"
                email = "franz@dropbox.com"
                email_verified = true

    example default
        is_parent_shared_folder_read_only = false
        last_modifier = default

patch struct FolderSharingInfo

    @common.InternalOnly
    is_parent_shared_folder_read_only Boolean?
        "Whether the parent shared folder is ready only. Set if the folder is contained by a shared folder."

    @common.InternalOnly
    is_team_folder Boolean?
        "Whether the current folder is a team folder. Set if the current folder is a shared folder."

    example default
        is_parent_shared_folder_read_only = false
        is_team_folder = false

    example shared_folder
        is_parent_shared_folder_read_only = false
        is_team_folder = false

patch struct FileMetadata

    @common.InternalOnly
    size_display String
        "A localized, human-readable string representing the size of the file, used for
        display purposes."

    @common.InternalOnly
    icon String
        "The name of the icon used to illustrate the file type in Dropbox's icon library."

    @common.InternalOnly
    mime_type String
        "The MIME type associated with the file."

    example default
        size_display = "5 bytes"
        icon = "folder"
        mime_type = "text/plain"

patch struct FolderMetadata

    @common.InternalOnly
    is_team_member_folder Boolean
        "Whether the folder is a CDM team member folder (i.e. the root folder of the team
        member's personal directory)."

    @common.InternalOnly
    is_shareable Boolean
        "Whether the current folder is a shared folder or is shareable."

    example default
        is_team_member_folder = false
        is_shareable = false

patch struct DeletedMetadata

    @common.InternalOnly
    is_dir Boolean
        "Whether the deleted entry was formerly a directory."

    example default
        is_dir = false

#
# Patches Changeset
#

struct ChangesetData
    ns_id UInt64
        "The namespace that was affected by the changes. Should only
        be used by internal clients."
    changeset_id UInt64
        "ID of the change that maps to our internal bookkeeping.
        Should only be used by internal clients."

    example default
        ns_id = 1
        changeset_id = 2

patch struct FileOpsResult

    @common.InternalOnly
    changeset_data List(ChangesetData)
        "A list of changeset data entries associated with a given file
        operation(s), used to undo the file operation(s). Sometimes, a valid
        file operation(s) cannot be undone, in which case, no :type:`ChangesetData`
        objects will be returned. This data should only be used by our internal clients."

    example default
        changeset_data = [default]

patch struct CreateFolderResult
    example default
        changeset_data = [default]

patch struct DeleteResult
    example default
        changeset_data = [default]

patch struct DeleteBatchResult
    example default
        changeset_data = [default]

patch struct RelocationResult
    example default
        changeset_data = [default]

patch struct RelocationBatchResult
    example default
        changeset_data = [default]

#
# Patches end
#

#
# Supported extensions
#

union PreviewType
    audio
        "Audio preview."
    file_tree
        "Archive preview."
    hls
        "HTTP Live Streaming preview."
    html
        "HTML preview."
    link_file
        "Link file (e.g. file representing web URL) preview."
    pdf
        "PDF preview."
    text
        "Text preview."
    thumbnail
        "Thumbnail preview."
    cloud_content
        "Cloud content preview."

    example default
        pdf = null

struct PreviewableExtensionInfo
    supported_preview_formats List(PreviewType)
        "The preview formats that are supported."
    full_content_preference List(PreviewType)
        "An ordered subset of supported extensions, used to determine client-side
        rendering behavior."

    example default
        supported_preview_formats = [default]
        full_content_preference = [default]

struct PreviewableExtensionsResult
    file_extension_to_previewable_info Map(String, PreviewableExtensionInfo)
        "Mapping from file extension to data that describes whether the extension
        is supported for previewing."

    example default
        file_extension_to_previewable_info = {"ai": default}

route get_previewable_extensions (Void, PreviewableExtensionsResult, Void)
    "Get supported file extensions for previewing."

    attrs
        owner = "api-platform"
        allow_app_folder_app = true

struct RollbackArg
    entries List(ChangesetData, min_items=1)
        "A list of changeset namespace data entries associated with a given file
        operation(s), used to undo the file operation(s)."

struct RollbackResult
    success Boolean
        "Whether the rollback was successful."

union RollbackError
    no_write_permission
        "Unable to rollback because user does not have the necessary write permissions."
    undo_operation_not_permitted
        "Unable to rollback because operation requires a namespace nest that is
        disallowed or unsupported."
    too_many_files
        "User is trying to undo an operation involving too many files."

route rollback_with_changesets (RollbackArg, RollbackResult, RollbackError)
    "Used to undo actions based on the supplied list of changesets."

struct Changeset
    ns_id UInt64
        "The namespace that was affected by the changes. Should only be used
        by out internal clients."
    changes List(UInt64)
        "IDs of the changes that map to our internal bookkeeping. Should only be
        used by our internal clients."

    example default
        ns_id = 1
        changes = [2]

struct InternalFileOpResult
    changesets List(Changeset)
    metadata Metadata

route delete_with_changeset (DeleteArg, InternalFileOpResult, DeleteError) deprecated by delete_v2
    "An internal version of :route:`delete` that returns changeset data for
    undoing file operations."

route upload_session/check_finish_batch_job_status(async.PollArg, UploadSessionFinishBatchJobStatus, async.PollError) deprecated by upload_session/finish_batch/check
    "An alias of :route:`upload_session/finish_batch/check`."

route upload_session/finish_batch_sync (UploadSessionFinishBatchArg, UploadSessionFinishBatchResult, Void)
    "sync version of :route:`upload_session/finish_batch`."

union UploadSessionFinishProcessedError extends UploadSessionFinishError
    processing_failed
        "Unable to apply the additional processing."

struct UploadSessionFinishProcessedArg extends UploadSessionFinishArg
    processing_type String?
        "The string that specifies the processing type."

    example ocr_pdf
        processing_type = "ocr_pdf"
        cursor = default
        commit = default

union_closed UploadSessionFinishProcessedResult
    success FileMetadata
        "File committed after the requested processing."
    failure UploadSessionFinishProcessedError
        "Unable to commit the file with the requested processing."

    example default
        success = default

union_closed UploadSessionFinishProcessedLaunch extends async.LaunchResultBase
    "Result returned by :route:`upload_session/finish_processed` that may either launch an
    asynchronous job or complete synchronously."

    complete UploadSessionFinishProcessedResult
        "The :route:`upload_session/finish_processed` has finished synchronously."

    example async_job_id
        async_job_id = "033a20f28cc4912e37756599f00782a4"

union_closed UploadSessionFinishProcessedJobStatus extends async.PollResultBase
    "Result returned by :route:`upload_session/finish_processed/check`."

    complete UploadSessionFinishProcessedResult
        "The :route:`upload_session/finish_processed` has finished asynchronously."

    example default
        complete = default

route upload_session/finish_processed (UploadSessionFinishProcessedArg, UploadSessionFinishProcessedLaunch, Void)
    "A variant of :route:`upload_session/finish` that performs additional mutation on the uploaded
    file asynchronously. The caller should check the status with
    :route:`upload_session/finish_processed/check`."

    attrs
        host = "content"
        style = "upload"
        is_preview = true

route upload_session/finish_processed/check(async.PollArg, UploadSessionFinishProcessedJobStatus, async.PollError)
    "Returns the status of an asynchronous job for :route:`upload_session/finish_processed`."

    attrs
        is_preview = true

route delete_batch_with_changeset_sync (DeleteBatchArg, InternalFileOpBatchResult, DeleteError) deprecated by delete_batch
    "This route helps you to delete multiple files/folders at once.

    Input should not contain duplicated/nested entries. If input contains
    both '/A' and '/A/B', this route won't be able to find '/A/B' after '/A'
    is deleted."

struct InternalFileOpBatchResult
    entries List(DeleteBatchResultEntry)
    changesets List(Changeset)

    example default
        entries = [default]
        changesets = [default]

route copy_batch_sync(RelocationBatchArg, RelocationBatchResult, RelocationBatchError)
    "Copy multiple files or folders to different locations at once in the
    user's Dropbox."

route move_batch_sync(RelocationBatchArg, RelocationBatchResult, RelocationBatchError)
    "Move multiple files or folders to different locations at once in the
    user's Dropbox."


#
# Browse folders routes
#
route tree_view_folders(InternalTreeViewFoldersArgs, InternalTreeViewFoldersResult, ListFolderError)
    "An internal version of browse_util/tree_view_folders AJAX endpoint."

struct InternalTreeViewFoldersArgs
    path PathR
        "The path to the folder you want to see the contents of."

    example default
        path = "/homework"

struct InternalTreeViewFoldersResult
    folders List(InternalTreeViewFoldersEntry)

    example default
        folders = [default]

struct InternalTreeViewFoldersEntry
    path PathR
        "The path of the sub folder."
    read_only Boolean
        "True when the folder is a read only folder."
    has_subdirs Boolean
        "True when the folder contains subfolders."
    is_shared Boolean = false
        "True when the folder is a shared folder."
    icon String
        "For web client, provides the icon file name for this folder."

    example default
        path = "/homework/math"
        read_only = false
        has_subdirs = true
        is_shared = false
        icon = "folder"


struct DialogInfoError
    title String
        "A user visible title for the problem."
    text String
        "A user visible description of the problem."
    learn_more String
        "A link to a help page to learn more about this problem."

    example default
        title = "Parent folder missing"
        text = "Continuing this operation will recreate /foo/bar, and this file will be visible to
                everyone with access to /foo"
        learn_more = "https://www.dropbox.com/help"

struct FSWDialogInfoError extends DialogInfoError
    id String
        "A unique ID for this type of FSW."
    blocking Boolean
        "True if this FSW is a blocking one and can't be confirmed."

    example default
        title = "Parent folder missing"
        text = "Continuing this operation will recreate /foo/bar, and this file will be visible to
                everyone with access to /foo"
        learn_more = "https://www.dropbox.com/help"
        id = "missing_parent"
        blocking = false

union FSWRequest
    check
        "Block actions on the standard set of file system warnings."
    confirm List(String)
        "A list of FSWs IDs that the user has previously confirmed and doesn't want to see.  These
        could be IDs that the user just confirmed or IDs where the user has selected a 'Do not ask
        me this again' checkbox."

struct FileSystemWarnings
    details List(FSWDialogInfoError)
        "A List of FSWDialogInfoErrors describing dialogs the user should be shown."

patch union WriteError

    @common.InternalOnly
    file_system_warnings FileSystemWarnings
        "If this action failed because one or more FSWs were triggered, this FileSystemWarnings
        object has details about each failure."

patch struct RelocationArg

    @common.InternalOnly
    fsw_request FSWRequest?
        "Specifies the FSW behavior the caller would like."

patch struct RelocationBatchArg

    @common.InternalOnly
    fsw_request FSWRequest?
        "Specifies the FSW behavior the caller would like."


#
# Restore path
#

struct RestorePathBatchArg
    entries List(RestorePath)
        "A list of paths to entries, where all paths have the same parent path."

    example default
        entries = ["/Homework/Prime_Numbers.txt", "/Homework/Square_Numbers.txt"]

union RestorePathBatchError
    path_lookup LookupError
        "An error occurs when looking up the metadata for the entry."
    path_write WriteError
        "An error occurs when trying to restore the entry."
    too_many_files
        "The operation would involve more than 10,000 files and folders."
    too_many_write_operations
        "There are too many write operations in user's Dropbox. Please retry
        this request."
    multiple_parent_paths
        "The entries have different parent paths."

struct RestorePathBatchResult
    count UInt32
        "Number of restored entries."

route restore_path_batch_sync(RestorePathBatchArg, RestorePathBatchResult, RestorePathBatchError)
    "Synchronously restore a set of deleted files or folders at a given path."

    attrs
        owner = "restorations-team"
        allow_app_folder_app = true
        select_admin_enabled = true
