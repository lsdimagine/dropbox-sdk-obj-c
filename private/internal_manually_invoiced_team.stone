namespace internal_manually_invoiced_team
    "Internal (as in, not sold by a reseller, but somebody on Dropbox Sales), manually invoiced (through i.e. Salesforce and not Moneytree) team management."

import internal_provisioner
import common

##### Types #####

alias Percentage = UInt64(min_value=0, max_value=100)

struct LicenseModification
    "Defines the old and new number of licenses."
    old_license_count UInt64(min_value=1)
    new_license_count UInt64(min_value=1)

    example upsell_five_to_eight
        old_license_count = 5
        new_license_count = 8

    example downsell_ten_to_six
        old_license_count = 10
        new_license_count = 6


##### Input/output Models #####

struct ProvisionTeamArgs
    "Common properties needed to provision a team, be that a brand new team or a Fastrak->DFB Provision."
    contact_email common.EmailAddress
        "Main point of contact for this team."
    plan_id UInt64
        "See metaserver/model/teams/plans/base.py to see all accepted team PlanIDs."
    billing_schedule_id UInt64
        "See metaserver/model/plans.py to see all accepted BillingScheduleIDs."
    price internal_provisioner.Price
        "Defines the price. For new trials, amount should be 0."
    billing_country internal_provisioner.CountryCode
        "The 2-letter country code of the country."
    billing_zip String?
        "If applicable - the zip code of the billing address.
         It will be used for tax eligibility/calculation."
    tax_exclusion internal_provisioner.TaxExclusion?
    external_invoice_id internal_provisioner.ExternalInvoiceId
        "The Salesforce Opportunity ID associated with this new-team request."


struct ProvisionNewTeamArgs extends ProvisionTeamArgs
    "Common properties needed to create a new team, both for Trial and Paid teams."
    team_name String(min_length=1)
        "The name of the new team."
    admin_emails List(common.EmailAddress, min_items=1)
        "List of emails for the first users to be provisioned."
    num_licenses UInt64(min_value=1)
        "The number of licenses to allocate to this team."


struct ChangeTeamPlanArgs extends ProvisionTeamArgs
    "Arguments to upgrade an existing team to a new plan, i.e. Fastrak to Biz, or Biz to Enterprise."

    team_id UInt64
        "The ID of the team being modified."
    license_modification LicenseModification
        "The old and new count of licenses. Can be an upsell or downsell."

    example default
        "An example request to upgrade a Fastrak team to DFB"
        contact_email = "wimax@emaildomain.com"
        plan_id = 101 # StandardTeamPlan
        billing_schedule_id = 2 # Yearly
        billing_country = "US"
        price = seven_hundred_usd
        external_invoice_id = salesforce_example
        team_id = 12345
        license_modification = upsell_five_to_eight


struct TeamStatusResponse
    team_id UInt64
    interval internal_provisioner.Interval
    plan_id UInt64
    billing_schedule_id UInt64
    num_licenses UInt64(min_value=1)
    num_provisioned_users UInt64

    example default
        "Sample response"
        team_id = 12345
        interval = default
        plan_id = 101
        billing_schedule_id = 2
        num_licenses = 5
        num_provisioned_users = 2


struct ProvisionNewTrialTeamArgs extends ProvisionNewTeamArgs
    "Arguments to construct a new trial team."

    trial_length UInt64(min_value=1)
        "Length of the trial, in days."

    example default
        "An example request to provision a new iwtrialid team"
        team_name = "My Cool Trial Team"
        contact_email = "wimax@emaildomain.com"
        plan_id = 101 # StandardTeamPlan
        billing_schedule_id = 2 # Yearly
        num_licenses = 5
        billing_country = "US"
        admin_emails = ["user@emaildomain.com", "user2@emaildomain.com"]
        trial_length = 45
        price = trial_costs_nothing
        external_invoice_id = salesforce_example


struct ProvisionNewPaidTeamArgs extends ProvisionNewTeamArgs
    "Arguments to construct a new paid team."

    example default
        "An example request to provision a new paid team"
        team_name = "My Cool Paid Team"
        contact_email = "wimax@emaildomain.com"
        plan_id = 101 # StandardTeamPlan
        billing_schedule_id = 2 # Yearly
        num_licenses = 5
        billing_country = "US"
        admin_emails = ["user@emaildomain.com", "user2@emaildomain.com"]
        price = seven_hundred_usd
        external_invoice_id = salesforce_example

struct TrialConvertOrRenewArgs
    team_id UInt64
        "The ID of the team being modified."
    external_invoice_id internal_provisioner.ExternalInvoiceId
        "The external payment ID associated with this team modification."
    price internal_provisioner.Price
        "Price of this trial conversion or renewal."
    license_modification LicenseModification?
        "The old and new count of licenses. Can be an upsell or downsell, or omit if there's no license modification."

    example trial_convert_to_yearly
        "An example trial conversion to a yearly team"
        team_id = 12345
        external_invoice_id = salesforce_example
        price = seventy_thousand_yen

    example renew_with_license_downsell
        "An example renewal with the optional license modification"
        team_id = 98765
        external_invoice_id = salesforce_example
        price = seven_hundred_usd
        license_modification = downsell_ten_to_six

struct UpsellArgs
    team_id UInt64
        "The ID of the team being modified."
    external_invoice_id internal_provisioner.ExternalInvoiceId
        "The external payment ID associated with this team modification."
    license_upsell LicenseModification
        "The old and new count of licenses. New count should be higher than old count."
    price internal_provisioner.Price
        "Price of this trial conversion or renewal. So if you're going from a 5 person team to a 6 person team, this should be the delta - roughly $150 USD."

    example upsell_five_to_eight
        team_id = 12345
        external_invoice_id = salesforce_example
        license_upsell = upsell_five_to_eight
        price = seven_hundred_usd


##### Routes #####

route provision_new_trial_team (ProvisionNewTrialTeamArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Provision a new trial team through SalesForce."
    attrs
        auth = "app"

route provision_new_paid_team (ProvisionNewPaidTeamArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Provision a new paid team through SalesForce."
    attrs
        auth = "app"

route trial_convert_team(TrialConvertOrRenewArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Convert a Trial to Paid."
    attrs
        auth="app"

route renew_team(TrialConvertOrRenewArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Rebill an existing paid team."
    attrs
        auth="app"

route upsell_team(UpsellArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Add licenses to an existing team."
    attrs
        auth="app"

route change_team_plan(ChangeTeamPlanArgs, TeamStatusResponse, internal_provisioner.InternalProvisionerError)
    "Upgrade an existing team to a new plan, i.e. Fastrak to Biz, or Biz to Enterprise."
    attrs
        auth="app"
