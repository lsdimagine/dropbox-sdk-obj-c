namespace starred

import home

#
# generic types
#

union FileIdType
    encoded_file_obj_id
    fq_path

union ItemIdTypeV2 extends FileIdType
    paper_document_id_path
    paper_folder_id_path

# These types are functionally equivalent to the corresponding ItemIdTypeV2 types with `path` suffix
union ItemIdType extends ItemIdTypeV2
    paper_document_id
    paper_folder_id

struct FileQuery
    "A file to query."

    id String
        "file_obj_id or fq_path of the file to operate on."

    id_type FileIdType = encoded_file_obj_id
        "What kind of identifier has been supplied, defaults to encoded_file_obj_id if none is specified."

    example default
        id = "12345"

    example with_explicit_type
        id = "/foo/bar.txt"
        id_type = fq_path

struct FileStarredStatus extends FileQuery
    "The starred status for a file."

    is_starred Boolean
        "Whether the file is starred."

    display_type home.DisplayType
        "The type of file/folder, for setting the icon and logging"

struct FileObjWithPath
    "Both of the identifiers (file_obj_id and fq_path) for a file."

    encoded_file_obj_id String
        "The encoded file_obj_id of the file."

    fq_path String
        "The fully qualified path to the file."

#
# route update_item
#
route update_item (UpdateItemArgs, Void, UpdateItemError) deprecated by update_item_v2
    "Change the starred status of a file."

    attrs
        owner="home"
        is_preview=true

struct UpdateItemArgs
    "Arguments for :route:`update_item`."

    id String
        "file_obj_id or fq_path of the item to operate on."

    id_type ItemIdType = encoded_file_obj_id
        "What kind of identifier has been supplied, defaults to encoded_file_obj_id if none is specified.
        Note: `paper_document_id` and `paper_document_id_path` are functionally equivalent, and both mean `paper_document_id_path`.
        Note: `paper_folder_id` and `paper_folder_id_path` are functionally equivalent, and both mean `paper_folder_id_path`.
        "

    is_starred Boolean
        "Whether the item is starred."

#
# route update_item_v2
#
route update_item_v2 (UpdateItemArgsV2, Void, UpdateItemError)
    "Change the starred status of a file."

    attrs
        owner="home"
        is_preview=true

struct UpdateItemArgsV2
    "Arguments for :route:`update_item_v2`."

    id String
        "file_obj_id or fq_path of the item to operate on."

    id_type ItemIdType = encoded_file_obj_id
        "What kind of identifier has been supplied, defaults to encoded_file_obj_id if none is specified."

    is_starred Boolean
        "Whether the item is starred."

    example default
        id = "12345"
        is_starred = true

    example unstar
        id = "12345"
        is_starred = false

    example with_explicit_type
        id = "12345"
        id_type = encoded_file_obj_id
        is_starred = false

    example with_fq_path_type
        id = "/dir/file.txt"
        id_type = fq_path
        is_starred = true

    example with_paper_type
        id = "/Xejfg92mx"
        id_type = paper_folder_id_path
        is_starred = true


union UpdateItemError
    file_not_found
        "The specified file could not be found."

#
# route check_status
#
route check_status(CheckStatusArgs, CheckStatusResponse, CheckStatusError)
    "Determine the starred status of a list of files."

    attrs
        owner="home"
        is_preview=true

struct CheckStatusArgs
    "Arguments for :route:`check_status`."

    files List(FileQuery)

struct CheckStatusResponse
    "Response for :route:`check_status`."

    starred_files List(FileStarredStatus)

union CheckStatusError
    unused

#
# route in_directory
#
route in_directory(InDirectoryArgs, InDirectoryResponse, InDirectoryError)
    "Fetches the starred items in a directory."

    attrs
        owner="home"
        is_preview=true

struct InDirectoryArgs extends FileQuery
    "Arguments for :route:`in_directory`."

struct InDirectoryResponse
    "Response for :route:`in_directory`."

    files List(FileObjWithPath)

union InDirectoryError
    directory_not_found
        "The specified directory could not be found."
