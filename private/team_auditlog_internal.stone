namespace team

import team_event_data_types
import common
import users

# EventType names should be the same as TeamEvent.EventType key
alias EventTypeName = String(pattern="[A-Z_]+")

union_closed TeamEventDataSelector
    paper_document team_event_data_types.PaperDocument
    paper_document_with_sharing_policy team_event_data_types.PaperDocumentWithSharingPolicy
    paper_document_with_subscription_level team_event_data_types.PaperDocumentWithSubscriptionLevel
    paper_folder_with_subscription_level team_event_data_types.PaperFolderWithSubscriptionLevel
    paper_folder team_event_data_types.PaperFolder
    paper_folder_with_document team_event_data_types.PaperFolderWithDocument
    paper_subfolder team_event_data_types.PaperSubfolder
    paper_recipient team_event_data_types.PaperRecipient
    paper_comment team_event_data_types.PaperComment

    example document
        paper_document = default

struct TeamEventArg
    event_type_name EventTypeName
    uuid String
        "Unique identifier of the event"
    actor_user_id users.AccountId
        "ID of the user taking the action (the admin if a user is being assumed)"
    actor_display_name common.DisplayName
        "Name of the actor user. (the admin if a user is being assumed)"
    event_data TeamEventDataSelector
        "Data to be stored for the event"
    assumed_user_id users.AccountId?
        "The assumed user if a Team Admin is acting on behalf of a user"
    ip_address String(pattern="^\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}\\.\\d{1,3}$")?
        "Used to force set IP for events generated remotely or on the server. Inferred from request if not set."
    timestamp common.DropboxTimestamp?
        "Timestamp of the event"

    example default
        event_type_name = "PAPER_DOC_CREATED"
        uuid = "aBC123def"
        actor_user_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        actor_display_name = "John Smith"
        event_data = document
        assumed_user_id = "dbid:BBH4f99T0taONIb-OurWxbNQ6ywGRopRynx"
        ip_address = "10.0.0.1"
        timestamp = "2016-09-16T21:59:26Z"

struct TeamEventResult
    complete Boolean
    duplicate Boolean?

    example default
        complete = true
        duplicate = false

union TeamEventError
    unknown_event_type
        "No EventType found. The provided event_type_name does not exist."
    invalid_actor_user_id
        "The user_id provided is not valid."

route log/log_event (TeamEventArg, TeamEventResult, TeamEventError)
    "Log Team events into the team activity log"
    attrs
        auth="team"
        owner="company-dropbox-team"
