namespace team

#
# Set team's paper policy
#

import team_administration

union DropboxTerms
    agree
        "Agree to Dropbox terms."

union_closed PaperDeploymentModeType
    full
        "All team members will be able to use paper."
    partial
        "Only selected team members will be able to use paper."

alias EnabledMembersUpdate = UpdateExcludedUsersArg

struct PaperDeploymentMode
    paper_deployment_type PaperDeploymentModeType = full
        "The paper deployment type."
    enabled_members_update EnabledMembersUpdate?
        "If :field:`paper_deployment_type` is `partial` then this can be used to update the
        list of enabled team members."

    example default
        paper_deployment_type = full


struct PaperPolicy
    paper_enabled Boolean
        "Whether to Allow people on the team to use Dropbox Paper."
    dropbox_terms DropboxTerms?
        "Whether to agree to Dropbox terms. This is required when :field:`paper_enabled` is :val:`true`."
    paper_deployment_mode PaperDeploymentMode?
        "Which team members will be able to use paper."

    example default
        paper_enabled = true
        dropbox_terms = agree
        paper_deployment_mode = default

alias PaperPolicyArg = PaperPolicy

union_closed PaperError extends team_administration.AdministrationError
    dropbox_terms_error
        "You must agree to dropbox terms in order to call the API."

route paper/set_paper_policy(PaperPolicyArg, Void, PaperError)
    "Settings the paper policy.

    Permission : Team member management."

    attrs
        auth="team"
        owner = "tools-and-insights"

#
# Set team's paper link policy
#

struct PaperLinkPolicy
    default_visibility team_administration.Visibility
        "Who do you want to be able to open links to Paper docs by default."

    example default
        default_visibility = public

alias PaperLinkPolicyArg = PaperLinkPolicy

route paper/set_paper_link_policy(PaperLinkPolicyArg, Void, AdministrationError)
    "Settings the default paper link policy.

    Permission : Team member management."

    attrs
        auth="team"
        owner = "tools-and-insights"

#
# Set team's paper sharing policy
#

struct PaperSharingPolicyArg
    external_sharing_allowed Boolean
        "Whether to allow team members to share paper docs and folders with people outside of the team."
    default_visibility team_administration.Visibility
        "Who do you want to be able to view Paper docs by default."

    example default
        external_sharing_allowed = true
        default_visibility = public

route paper/set_paper_sharing_policy(PaperSharingPolicyArg, Void, team_administration.AdministrationError)
    "Setting the paper sharing policy.

    Permission : Team member management."

    attrs
        auth="team"
        owner = "tools-and-insights"
