namespace users

import common

import users_common

#
# Sharing preferences
#

union GetSharingPrefsError
    "Errors that happen when getting sharing preferences."

union SetSharingPrefsError
    "Errors that happen when setting sharing preferences."

struct SharingPrefs
    "Information about a user's sharing preferences.
    Optional fields will always be returned - they are optional for setting only."

    add_message_as_comment_checked Boolean?
        "If the \"Add as message\" checkbox should be checked by default."
    add_message_as_comment_help_shown Boolean?
        "If the help for the \"Add as message\" checkbox should be shown."
    access_reader_no_comment_content_links Boolean?
        "If we should allow the user to add users with a no comment role for
        shared content links."
    access_reader_no_comment_shared_folders Boolean?
        "If we should allow the user to add users with a no comment role for
        shared folders."

route get_sharing_prefs(Void, SharingPrefs, GetSharingPrefsError)
    "Get the sharing preferences for the current user."

route set_sharing_prefs(SharingPrefs, Void, SetSharingPrefsError)
    "Set sharing preferences for the curent user."

route get_root_info(Void, RootInfo, Void)
    "Temporary private route to expose CDM information."

    attrs
        owner = "api-platform"

union RootInfo
    cdm_team CDMInfo
        "The user is on a CDM-enabled team."
        struct
            @common.PathRedactor
            path_root String
                "The ID of the team shared Dropbox."

            @common.PathRedactor
            home_path String
                "The path relative to the team shared Dropbox to the user's
                team member folder."

            home_ns_id String
                "The ID of the user's TMF."
    not_on_cdm_team String
        "The user is not on a CDM-enabled team, but we'll return their home_ns_id."

#
# Route: get_plan_info
#

struct GetPlanInfoArg
    account_id users_common.AccountId
        "A user's account identifier."

    example default
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"

struct PlanInfo
    "The amount of detail revealed about an account depends on the user being queried."
    account_id users_common.AccountId
        "A user's account identifier."
    account_type users_common.AccountType
        "What type of account this user has."
    plan_text String
        "User facing string for plan name."
    plan_detail_text String?
        "User facing string for detailed plan text."
    is_trial Boolean?
        "Is the user on a trial plan."

    example default
        "Basic account."
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        account_type = basic
        plan_text = "Dropbox Basic"

    example plus_subscriber
        "A personal account that has a plus subscription."
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        account_type = pro
        plan_text = "Dropbox Plus"

    example ios_plus_subscriber
        "A personal account that has a plus subscription on Apple ITunes."
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        account_type = pro
        plan_text = "Dropbox Plus"
        plan_detail_text = "Dropbox IOS Plus"

    example ios_plus_trial_subscriber
        "A personal account that has a plus trial subscription on Apple ITunes."
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        account_type = pro
        plan_text = "Dropbox Plus"
        plan_detail_text = "Dropbox IOS Plus Trial"
        is_trial = true

    example user_on_business_team
        "A user that's a part of a team."
        account_id = "dbid:AAH4f99T0taONIb-OurWxbNQ6ywGRopQngc"
        account_type = business
        plan_text = "Dropbox Business"

union GetPlanInfoError
    no_account
        "The specified :field:`GetPlanInfoArg.account_id` does not exist."

route get_plan_info(GetPlanInfoArg, PlanInfo, GetPlanInfoError)
    "Get information about a user's plan."

    attrs
        owner = "cash-team"

#
# Scale mitigation endpoint for desktop client
#

struct ScaleMitigationResult
    needs_scale_mitigation Boolean
        "Whether the user needs scale mitigation."
    reason ScaleMitigationReason?
        "Included if scale mitigation is necessary, this is the reason why it is needed."

    example default
        needs_scale_mitigation = false

union ScaleMitigationReason
    too_many_files TooManyFilesReason
        "User needs scale mitigation because they have too many files."

struct TooManyFilesReason
    approximate_file_count Int64
        "The approximate number of files the user has in their Dropbox."
    file_limit Int64
        "The file limit that the user has surpassed."

union ScaleMitigationError
    "Errors that happen when getting if the user needs scale mitigation."

route needs_scale_mitigation(Void, ScaleMitigationResult, ScaleMitigationError)
    "Get information if the user needs scale mitigation for the desktop client to work properly."

    attrs
        owner = "sync-experience"

#
# Mobile client preferences
#

union MobileClientPlatform
    ios
    android
    windows

union GetMobileClientSettingsError
    "Errors that occur when getting the mobile client settings."
    no_such_user
        "In case the user_id doesn't exist when someone call the API directly."
    unknown_setting_type

union SaveMobileClientSettingsError
    "Errors that occur when setting the mobile client settings."
    setting_name_or_value_too_long
        "Setting names or values are too long."
    no_such_user
        "In case the user_id doesn't exist when someone call the API directly."
    too_many_settings
        "Returned when the API user requests too many settings at one time."
    unknown_setting_type

struct GetMobileClientDeviceSettingsInfoArg
    user_id String
        "Authenticated user's Dropbox user ID."
    setting_type String
        "Mobile client settings type contains: camera_uploads, offline_files, password_lock etc."
    device_id String

    example default
        user_id = "8"
        setting_type = "camera_uploads"
        device_id = "D8717F776-9241-4929-8ABA-94CA3B8EEE63"

struct GetMobileClientDeviceSettingsInfoResult
    "The infomation correspondes to the items on Settings page."
    user_id String
        "Authenticated user's Dropbox user ID."
    setting_type String
        "Mobile client settings type contains: camera_uploads, offline_files, password_lock etc."
    settings Map(String, String)
        "Key is setting_name;  Value is setting_value."
    mobile_client_platform MobileClientPlatform
    device_id String
    device_name String

    example default
        user_id = "8"
        setting_type = "camera_uploads"
        settings = {"Camera Uploads Enabled":"ON"}
        mobile_client_platform = ios
        device_id = "8717F776-9241-4929-8ABA-94CA3B8EEE63"
        device_name = "yuzhong-mbp"

struct SaveMobileClientDeviceSettingsInfoArg
    "The infomation corresponds to the items on Settings page."
    user_id String
        "Authenticated user's Dropbox user ID."
    setting_type String
        "Mobile client settings type contains: camera_uploads, offline_files, password_lock etc."
    settings Map(String, String)
        "Key is setting_name;  Value is setting_value."
    mobile_client_platform MobileClientPlatform
    device_id String
    device_name String

    example default
        user_id = "8"
        setting_type = "camera_uploads"
        settings = {"Camera Uploads Enabled":"ON"}
        mobile_client_platform = ios
        device_id = "8717F776-9241-4929-8ABA-94CA3B8EEE63"
        device_name = "yuzhong-mbp"

struct SaveMobileClientDeviceSettingsInfoResult
    "This is for backwards-compatible changes."
    example default

route get_mobile_client_settings(GetMobileClientDeviceSettingsInfoArg, GetMobileClientDeviceSettingsInfoResult, GetMobileClientSettingsError)
    "Get a specific type of settings of one device for the user."

route save_mobile_client_settings(SaveMobileClientDeviceSettingsInfoArg, SaveMobileClientDeviceSettingsInfoResult, SaveMobileClientSettingsError)
    "Save a collection of client settings to Edgestore."
