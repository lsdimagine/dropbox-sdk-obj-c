namespace file_activity_stream

import common

union FileActivityStreamAccessType
    view_name_only
    traverse
    reader_no_comment
    reader
    writer
    owner

union SharedLinkAudience
    off
    team
    public

struct FileActivityStreamUser
    id String
    display_name String
    email String?
    photo_url String?

struct FileActivityStreamGroup
    id String
    is_team_group Boolean
    name String
    user_count UInt32

struct Activity
    union
        add                          AddActivity
        edit                         EditActivity
        move                         MoveActivity
        rename                       RenameActivity
        shared_content_invite        SharedContentInviteActivity
        shared_link                  SharedLinkActivity
        view                         ViewActivity

    id String
    timestamp common.DropboxTimestamp
    user FileActivityStreamUser

struct AddActivity extends Activity
    "Activity for added files."

    path String

struct EditActivity extends Activity
    "Activity for edited files."

    revision_num UInt32

struct MoveActivity extends Activity
    "Activity for moved files."

    path String
    last_path String

struct RenameActivity extends Activity
    "Activity for renamed files."

    filename String
    last_filename String

struct ViewActivity extends Activity
    "Activity for viewed files."

struct SharedContentInviteActivity extends Activity
    "Activity for shared files."

    shared_emails List(String)
    shared_groups List(FileActivityStreamGroup)
    shared_users List(FileActivityStreamUser)
    access_type FileActivityStreamAccessType

struct SharedLinkActivity extends Activity
    "Activity for shared links."

    link_audience SharedLinkAudience


struct GetActivityStreamRequest
    file_id String
    cursor String?

struct GetActivityStreamResponse
    activity List(Activity)

union FileActivityStreamError
    no_file
        "The file does not exist."
    no_permission
        "The user does not have permission to view this file."

route get_activity_stream (GetActivityStreamRequest, GetActivityStreamResponse, FileActivityStreamError)

struct FeedbackRequest
    feedback Boolean
    file_preview_session_id String
    file_viewer_session_id String
    file_ns_id UInt32
    file_sjid UInt32

struct FeedbackResponse
    success Boolean

union FeedbackError
    no_permission
        "The user does not have permission to view this file."
    already_provided
        "The user has already provided feedback."

route feedback (FeedbackRequest, FeedbackResponse, FeedbackError)
