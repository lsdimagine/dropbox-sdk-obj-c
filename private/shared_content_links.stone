namespace sharing

import common


struct GetLinkMetadataArg
    url String
        "The URL of the link."

union GetLinkMetadataError
    access_denied
        "The user does not have permission to get the metadata of this link."

struct SetLinkSettingsArg
    "Modifies the link settings of the link."
    link_settings AlphaLinkSettings
    url String
        "The URL of the link"

union SetLinkSettingsError
    access_denied
        "The user does not have permission to set settings on this link."

route alpha/get_link_metadata(GetLinkMetadataArg, SharedContentLinkMetadata, GetLinkMetadataError) deprecated
    "DEPRECATED: talk to davy@, use alpha/get_file_metadata and alpha/get_folder_metadata instead

    Retrieves the link metadata of the link."

    attrs
        api_group="sharing"
        is_preview = true
        owner = "sharing"

route alpha/set_link_settings(SetLinkSettingsArg, SharedContentLinkMetadata, SetLinkSettingsError) deprecated
    "DEPRECATED: talk to davy@, use alpha/update_file_policy and alpha/update_folder_policy instead

    Modifies the link settings of the link."

    attrs
        api_group="sharing"
        is_preview = true
        owner = "sharing"

struct AlphaLinkSettings
    "Settings that apply to a link."
    access_level AccessLevel?
        "The access level on the link for this file."
    audience AlphaLinkAudience?
        "The type of audience on the link for this file."
    expiry LinkExpiry?
        "An expiry timestamp to set on a link."
    password LinkPassword?
        "The password for the link."

union AlphaLinkAudience extends LinkAudience
    default_on
        "Link is accessible only by the default audience."

struct ExpectedSharedContentLinkMetadata extends SharedContentLinkMetadataBase
    "Metadata of a shared link for a file or folder."
    audience_restricting_shared_folder AudienceRestrictingSharedFolder?
        "The shared folder that prevents the link audience for this link from being more
        restrictive."

    example default
        audience_options = [public, team, members]
        current_audience = public
        link_permissions = [default]
        password_protected = false

struct AlphaSharedContentLinkMetadata extends SharedContentLinkMetadata
    audience_restricting_shared_folder AudienceRestrictingSharedFolder?
        "The shared folder that prevents the link audience for this link from being more
        restrictive."

    example default
        audience_options = [public, team, members]
        current_audience = public
        link_permissions = [default]
        password_protected = false
        url = ""

struct AudienceRestrictingSharedFolder
    "Information about the shared folder that prevents the link audience for this link from being
    more restrictive."
    shared_folder_id common.SharedFolderId
        "The ID of the shared folder."
    name String
        "The name of the shared folder."
    audience LinkAudience
        "The link audience of the shared folder."
