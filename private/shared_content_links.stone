namespace sharing

import common

union LinkAudience
    public
        "Link is accessible by anyone."
    team
        "Link is accessible only by team members."
    members
        "Link is accessible only by members of the content."

union LinkAction
    "Actions that can be performed on a link."
    change_audience
        "Change the audience of the link."
    remove_expiry
        "Remove the expiry date of the link."
    remove_password
        "Remove the password of the link."
    set_expiry
        "Create or modify the expiry date of the link."
    set_password
        "Create or modify the password of the link."

union AlphaPermissionDeniedReason extends PermissionDeniedReason
    "The reason the user is not allowed to perform the action on the link."
    owner_not_on_team
        "The content owner needs to be on a Dropbox team to perform this action."
    permission_denied
        "The user does not have permission to perform this action on the link."
    restricted_by_team
        "The user's team policy prevents performing this action on the link."
    user_account_type
        "The user's account type does not support this action."
    user_not_on_team
        "The user needs to be on a Dropbox team to perform this action."

struct LinkPermission
    "Permissions for actions that can be performed on a link."
    action LinkAction
    allow Boolean
    reason AlphaPermissionDeniedReason?

union LinkExpiry
    remove_expiry
        "Remove the currently set expiry for the link."
    set_expiry common.DropboxTimestamp
        "Set a new expiry or change an existing expiry."

union LinkPassword
    remove_password
        "Whether to remove the currently set password for the link."
    set_password String
        "Set a new password or change an existing password."

struct LinkSettings
    "Settings that are placed on a link to a piece of content."
    audience LinkAudience?
        "The type of audience on the link for this file"
    access_level AccessLevel?
        "The type of access level for the link."
    expiry LinkExpiry?
        "An expiry timestamp to set on a link."
    password LinkPassword?
        "The password for the link."

struct SharedContentLinkMetadata
    "Metadata of a shared link for a file or folder."
    audience_options List(LinkAudience)
        "The audience options that are available for the content. Some audience options may be
        unavailable. For example, team_only may be unavailable if the content is not owned by a
        user on a team."
    current_audience LinkAudience
        "The current audience of the link."
    expiry common.DropboxTimestamp?
        "Whether the link has an expiry set on it. A link with an expiry will have its
         audience changed to members_only when the expiry is reached."
    link_permissions List(LinkPermission)
        "A list of permissions for actions you can perform on the link."
    password_protected Boolean
        "Whether the link is protected by a password."
    url String
        "The URL of the link."

struct GetLinkMetadataArg
    url String
        "The URL of the link."

union GetLinkMetadataError
    access_denied
        "The user does not have permission to get the metadata of this link."

struct SetLinkSettingsArg
    "Modifies the link settings of the link."
    link_settings LinkSettings
    url String
        "The URL of the link"

union SetLinkSettingsError
    access_denied
        "The user does not have permission to set settings on this link."

route alpha/get_link_metadata(GetLinkMetadataArg, SharedContentLinkMetadata, GetLinkMetadataError)
    "Retrieves the link metadata of the link."

    attrs
        api_group="sharing"
        is_preview = true
        owner = "sharing"

route alpha/set_link_settings(SetLinkSettingsArg, SharedContentLinkMetadata, SetLinkSettingsError)
    "Modifies the link settings of the link."

    attrs
        api_group="sharing"
        is_preview = true
        owner = "sharing"
