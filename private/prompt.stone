namespace prompt
    "This namespace contains endpoints for retrieving and interacting with
    PROMPT campaigns."


route get_best_campaigns(GetBestCampaignsArg, CampaignsResult, Void)
    "Get the best PROMPT campaigns for the given user."

route get_campaigns(GetCampaignsArg, CampaignsResult, CampaignErr)
    "Get the PROMPT campaigns that match campaign names in GetCampaignsArg for the user."

route confirm(CampaignArg, Void, CampaignErr)
    "Mark a campaign as confirmed."

route dismiss(CampaignArg, Void, CampaignErr)
    "Mark a campaign as dimsissed."

route log_impression(CampaignArg, Void, CampaignErr)
    "Log a client side impression of a campaign."


alias CampaignId = Int64
alias PromptTimestamp = UInt64
alias VersionId = Int64

union TopNotificationStyle
    info
        "Informational notification style."
    notification
        "Regular notification style."
    warning
        "Warning notification style."
    promo
        "Promo notification style."

union DesktopTrayStyle
    info
        "Informational notification style."
    notification
        "Regular notification style."
    warning
        "Warning notification style."

struct MobilePromptOpenUrlActionParameters
    url String

struct MobilePromptOpenUrlAction
    action_parameters MobilePromptOpenUrlActionParameters

union MobilePromptAction
    open_recents_tab
    open_photos_tab
    open_payments_page
    open_link_computer_page
    open_camera_upload_settings
    open_doc_scanner
    open_url MobilePromptOpenUrlAction

union TopNotificationIcon
    info

struct TopNotification
    text String
    subtext String?
    style TopNotificationStyle
    action MobilePromptAction?
    icon TopNotificationIcon?
    icon_url String?
    not_dismissable Boolean = false

    example default
        text = "You are running out of space."
        subtext = "You are on a basic account."
        style = notification
        action = open_recents_tab
        icon = info
        icon_url = ""
        not_dismissable = false

struct TopNotificationWithModal
    top_notification TopNotification
    modal Modal

struct PopupModal
    text String
    subtext String
    image_url String
    dismiss_text String
    confirm_action MobilePromptAction
    confirm_text String

struct DesktopTray
    text String
    subtext String
    style DesktopTrayStyle
    icon_url String?
    confirm_url String

    example default
        text = "Your hard drive just got infinitely bigger"
        subtext = "Learn more about dropbox infinite."
        style = notification
        icon_url = "/static/image/prompt/desktop/cloud.svg"
        confirm_url = "/home"

union ModalStyle
    default
        "Modal with a title, subtitle, image, cancel button, action button, and an optional second action button."
    stretched
        "Default modal with an image that is stretched to span the entire width of the modal."

struct Modal
    title String
    text String
    subtext String
    style ModalStyle
    image_url String
    dismiss_text String
    confirm_action_1 MobilePromptAction
    confirm_text_1 String
    confirm_action_2 MobilePromptAction?
    confirm_text_2 String?

struct UpgradePage
    title String
    image_url String
    text String
    subtext String
    benefit_list List(String)
    action_text String

union CampaignContent
    top_notification TopNotification
    top_notification_with_modal TopNotificationWithModal
    popup_modal PopupModal
    desktop_tray DesktopTray

    upgrade_page UpgradePage

    example default
        top_notification = default

struct CampaignResult
    matched_user_id Int64?
    version_id VersionId?
    campaign_id CampaignId
    prompt_queried_at_ms PromptTimestamp
    campaign_name String
    content CampaignContent

    example default
        campaign_id = 499
        prompt_queried_at_ms = 1484012717394
        campaign_name = "over-quota-top-notification"
        content = default

union RefAction
    files_personal
    files_work
    recents_personal
    recents_work
    offline_personal
    offline_work
    photos_personal
    photos_work

struct GetBestCampaignsArg
    action RefAction?
    match_user_ids List(Int64)?

    example default
        action = files_personal
        match_user_ids = [42, 84]

struct GetCampaignsArg
    campaign_names List(String)

    example default
        campaign_names = ["mobile-dropbox-plus", "mobile-dropbox-free-trial"]

struct CampaignsResult
    campaigns List(CampaignResult)
    valid_for UInt64
    request_id String?
        "request_id is a string that uniquely identifies the request during
        which this campaigns result was constructed. I.e. the request that
        found and returned this campaigns result back to a client. Typically,
        clients take the request_id and echo it back to us as
        prompt_parent_request_id anytime they notify us about campaign
        impressions and campaign interactions. Together, request_id and
        prompt_parent_request_id let us associate client events (client
        impression, confirm, dismiss) with server events (impression)."


    example default
        campaigns = [default]
        valid_for = 0

struct CampaignArg
    campaign_id CampaignId
    version_id VersionId?
    prompt_queried_at_ms PromptTimestamp?
    prompt_parent_request_id String?

    example default
        campaign_id = 499

union CampaignErr
    not_found String
        "A campaign couldn't be found."

    bad_platform String
        "The found campaign version doesn't belong to the mobile platform."

    bad_match_user_ids String
        "Campaigns are requested for a user which isn't owned by the authenticated user."

    example default
        not_found = "There is no campaign version with the id '499'."

    example bad_platform
        bad_platform = "A campaign version was found for id '499' but it doesn't belong to the mobile platform."

    example bad_match_user_ids
        bad_match_user_ids = "The user with id '499' is not owned by the requesting user."
