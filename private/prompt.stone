namespace prompt
    "This namespace contains endpoints for retrieving and interacting with
    PROMPT campaigns."


route get_best_campaigns(GetBestCampaignsArg, CampaignsResult, Void)
    "Get the best PROMPT campaigns for the given user."

route confirm(CampaignArg, Void, CampaignErr)
    "Mark a campaign as confirmed."

route dismiss(CampaignArg, Void, CampaignErr)
    "Mark a campaign as dimsissed."

route log_impression(CampaignArg, Void, CampaignErr)
    "Log a client side impression of a campaign."


alias CampaignId = Int64
alias PromptTimestamp = UInt64

union TopNotificationStyle
    info
        "Informational notification style."
    notification
        "Regular notification style."
    warning
        "Warning notification style."
    promo
        "Promo notification style."

struct MobilePromptOpenUrlActionParameters
    url String

struct MobilePromptOpenUrlAction
    action_parameters MobilePromptOpenUrlActionParameters

union MobilePromptAction
    open_recents_tab
    open_photos_tab
    open_payments_page
    open_link_computer_page
    open_camera_upload_settings
    open_doc_scanner
    open_url MobilePromptOpenUrlAction

union TopNotificationIcon
    info

struct TopNotification
    text String
    subtext String?
    style TopNotificationStyle
    action MobilePromptAction?
    icon TopNotificationIcon?
    icon_url String?
    not_dismissable Boolean = false

    example default
        text = "You are running out of space."
        subtext = "You are on a basic account."
        style = notification
        action = open_recents_tab
        icon = info
        icon_url = ""
        not_dismissable = false

struct TopNotificationWithModal
    top_notification TopNotification
    modal Modal

union ModalStyle
    default
        "Modal with a title, subtitle, image, cancel button, action button, and an optional second action button."
    stretched
        "Default modal with an image that is stretched to span the entire width of the modal."

struct Modal
    title String
    text String
    subtext String
    style ModalStyle
    image_url String
    dismiss_text String
    confirm_action_1 MobilePromptAction
    confirm_text_1 String
    confirm_action_2 MobilePromptAction?
    confirm_text_2 String?

union CampaignContent
    top_notification TopNotification
    top_notification_with_modal TopNotificationWithModal

    example default
        top_notification = default

struct CampaignResult
    campaign_id CampaignId
    prompt_queried_at_ms PromptTimestamp
    campaign_name String
    content CampaignContent

    example default
        campaign_id = 499
        prompt_queried_at_ms = 1484012717394
        campaign_name = "over-quota-top-notification"
        content = default

union RefAction
    files_personal
    files_work
    recents_personal
    recents_work
    offline_personal
    offline_work
    photos_personal
    photos_work

struct GetBestCampaignsArg
    action RefAction

    example default
        action = files_personal

struct CampaignsResult
    campaigns List(CampaignResult)
    valid_for UInt64

    example default
        campaigns = [default]
        valid_for = 0

struct CampaignArg
    campaign_id CampaignId
    prompt_queried_at_ms PromptTimestamp?
        "Optional for backward compatibility."

    example default
        campaign_id = 499

union CampaignErr
    not_found String
        "A campaign couldn't be found."

    bad_platform String
        "The found campaign doesn't belong to the mobile platform."

    example default
        not_found = "There is no campaign with the id '499'."

    example bad_platform
        bad_platform = "A campaign was found for id '499' but it doesn't belong to the mobile platform."
